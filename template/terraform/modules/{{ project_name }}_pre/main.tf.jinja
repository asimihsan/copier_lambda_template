terraform {
  backend "s3" {}
}

provider "aws" {
  region = var.region
}

resource "aws_dynamodb_table" "deployment_rotation" {
  name         = "${var.project_prefix}-deployment-rotation"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "creator_team_id"
  range_key    = "rotation_label"

  attribute {
    name = "creator_team_id"
    type = "S"
  }

  attribute {
    name = "rotation_label"
    type = "S"
  }

  point_in_time_recovery {
    enabled = true
  }
}

resource "aws_dynamodb_table" "rotation_schedule" {
  name         = "${var.project_prefix}-rotation-schedule"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "scheduled_hour"
  range_key    = "event_id"

  attribute {
    name = "scheduled_hour"
    type = "S"
  }

  attribute {
    name = "event_id"
    type = "S"
  }

  ttl {
    attribute_name = "expires_at"
    enabled        = true
  }

  point_in_time_recovery {
    enabled = true
  }
}

resource "aws_dynamodb_table" "override_requests" {
  name         = "${var.project_prefix}-override-requests"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "override_id"

  attribute {
    name = "override_id"
    type = "S"
  }

  point_in_time_recovery {
    enabled = true
  }
}

resource "aws_iam_role" "lambda_exec_role" {
  name               = "${var.project_prefix}-lambda-role"
  assume_role_policy = jsonencode({
    Version   = "2012-10-17",
    Statement = [{
      Action    = "sts:AssumeRole",
      Effect    = "Allow",
      Principal = { Service = "lambda.amazonaws.com" }
    }]
  })
}

resource "aws_iam_policy_attachment" "lambda_policy_attach" {
  name       = "${var.project_prefix}-lambda-policy"
  roles      = [aws_iam_role.lambda_exec_role.name]
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
}

resource "aws_iam_policy" "lambda_dynamodb_policy" {
  name        = "${var.project_prefix}-lambda-dynamodb-policy"
  description = "IAM policy to allow Lambda functions to access DynamoDB tables"
  policy      = jsonencode({
    Version   = "2012-10-17",
    Statement = [
      {
        Effect = "Allow",
        Action = [
          "dynamodb:GetItem",
          "dynamodb:PutItem",
          "dynamodb:UpdateItem",
          "dynamodb:DeleteItem",
          "dynamodb:Query",
          "dynamodb:Scan",
        ],
        Resource = [
          aws_dynamodb_table.deployment_rotation.arn,
          aws_dynamodb_table.override_requests.arn,
          aws_dynamodb_table.rotation_schedule.arn,
        ]
      }
    ]
  })
}

resource "aws_iam_policy_attachment" "lambda_dynamodb_policy_attach" {
  name       = "${var.project_prefix}-lambda-dynamodb-policy-attach"
  roles      = [aws_iam_role.lambda_exec_role.name]
  policy_arn = aws_iam_policy.lambda_dynamodb_policy.arn
}
